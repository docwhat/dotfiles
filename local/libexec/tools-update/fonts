#!/bin/zsh

#DESC# fonts for macOS #DESC#

set -euo pipefail

cd "$(dirname "$0")"

typeset -r release='v1.0.0'
typeset -a fonts

fonts=(
FiraCode
Hack
SourceCodePro
)

fetch_or_update() {
  local url destination
  url=$1
  destination=$2
  if [ ! -r "$destination" ]; then
    curl -sSfL -o "$destination" "$url"
  else
    curl -sSfL -z "$destination" -o "$destination" "$url"
  fi
}

## Main

if [[ "${OSTYPE}" != darwin* ]]; then
  echo "${OSTYPE} is not supported\!" 1>&2
  exit 0
fi

mkdir -p ~/tmp/.fonts/
for file in ~/Library/Fonts/*Nerd*(.N); do
  rm "${file}"
done

for font in "${fonts[@]}"; do
  zipfile=~/tmp/.fonts/"${font}.zip"
  fetch_or_update \
    "https://github.com/ryanoasis/nerd-fonts/releases/download/${release}/${font}.zip" \
    "${zipfile}"
  unzip -uo "${zipfile}" -d ~/Library/Fonts/ -x '*Windows*' '*Mono.*'
done

# Check for updates (human)
open https://github.com/ryanoasis/nerd-fonts/releases

# EOF
