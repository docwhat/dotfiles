#!/bin/zsh

#DESC# fonts for macOS #DESC#

set -euo pipefail

cd "$(dirname "$0")"

typeset -r release='v1.1.0'
typeset -a fonts
typeset -r cachedir="$HOME/.cache/tools-update/fonts"

fonts=(
FiraCode
Hack
SourceCodePro
)

fetch_or_update() {
  local url destination
  url=$1
  destination=$2
  if [ ! -r "$destination" ]; then
    curl -sSfL -o "$destination" "$url"
  else
    curl -sSfL -z "$destination" -o "$destination" "$url"
  fi
}

## Main

if [[ "${OSTYPE}" != darwin* ]]; then
  print -P "  %F{yellow}Skipping%F{reset}: Only %F{green}macOS%F{reset} supported (not %F{green}${OSTYPE}%F{reset})"
  exit 0
fi

mkdir -p "$cachedir" ~/Library/Fonts/NerdFonts

for file in ~/Library/Fonts/NerdFonts/*(.N); do
  rm "${file}"
done

for font in "${fonts[@]}"; do
  zipfile="${cachedir}/${font}.zip"
  fetch_or_update \
    "https://github.com/ryanoasis/nerd-fonts/releases/download/${release}/${font}.zip" \
    "${zipfile}"
  unzip -uo "${zipfile}" -d ~/Library/Fonts/NerdFonts -x '*Windows*' '*Mono.*'
done

# Check for updates (human)
# open https://github.com/ryanoasis/nerd-fonts/releases

# EOF
