#!/usr/bin/env zsh
#DESC# python packages (2 & 3)#DESC#

set -euo pipefail

if [ $UID = 0 ]; then
  exit 0
fi

DOTFILES_DIR=~/.dotfiles
source "${DOTFILES_DIR}/local/include/hook-helpers.zsh"

declare -raU eggs=(
flake8
isort
neovim
pip
wheel
)

declare -raU eggs2=(
"${(@)eggs}"
)

declare -raU eggs3=(
"${(@)eggs}"
black
jedi
pip
proselint
psutil
pyaml
python-language-server
ruamel.yaml.cmd
setproctitle
vim-vint
wheel
yamllint
)

function python_major_version() {
  declare -r python="${commands[$1]}"

  if [ -z "$python" ]; then
    echo 0
  else
    "$python" -c 'import sys; print(sys.version_info[0])'
  fi
}

declare -A pythons
# This order is important. We want python2/3 to clobber python.
for python in python python2 python3; do
  k="$(python_major_version "$python")"
  pythons[$k]="${commands[$python]}"
done
declare -r pythons

for k in "${(@k)pythons}"; do
  declare eggvar="eggs${k}"
  declare python="${pythons[$k]}"
  declare -a args

  echo "PYTHON ${k}" | colorize green

  args=(
  "$python"
  -m pip
  install
  --user
  --upgrade
  "${(@P)eggvar}"
  )

  "${(@)args}" | offset
done

# EOF
