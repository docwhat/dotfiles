#!/usr/bin/env zsh
#DESC# python packages (2 & 3)#DESC#

set -euo pipefail

if [ $UID = 0 ]; then
  exit 0
fi

DOTFILES_DIR=~/.dotfiles
source "${DOTFILES_DIR}/local/include/hook-helpers.zsh"

eggs=(
black
flake8
isort
jedi
neovim
pip
proselint
psutil
pyaml
python-language-server
ruamel.yaml.cmd
setproctitle
vim-vint
wheel
yamllint
)

pips=( pip3 pip2 pip )

# Reduce the search list if both exist (pip will be a duplicate of one of them).
if (( $+commands[pip3] )) && (( $+commands[pip2] )); then
  pips=( pip3 pip2 )
fi

for pip in "${pips[@]}"; do
  (( $+commands[$pip] )) || continue

  echo "${commands[${pip}]}" | colorize green

  args=( "${commands[$pip]}" install --upgrade )

  if [[ "${commands[$pip]}" = "$HOME"/* ]]; then
    args+=( --user )
  fi

  "${args[@]}" "${eggs[@]}" | offset
done

# EOF
