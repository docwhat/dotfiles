#!/usr/bin/env zsh
#DESC# python packages (2 & 3)#DESC#

set -euo pipefail

if [ $UID = 0 ]; then
  exit 0
fi

DOTFILES_DIR=~/.dotfiles
source "${DOTFILES_DIR}/local/include/hook-helpers.zsh"

function python_major_version() {
  declare -r python=$1
  declare -r pythonpath=${commands[$python]:-}

  if [ -n "$pythonpath" ]; then
    "$pythonpath" -c 'import sys; print(sys.version_info[0])'
  fi
}

declare -A pythons
# This order is important. We want python2/3 to clobber python.
for python in python python2 python3; do
  k="$(python_major_version "$python")"
  if [ -n "$k" ]; then
    pythons[$k]=$commands[$python]
  fi
done
declare -r pythons

for k in "${(@k)pythons}"; do
  declare python="${pythons[$k]}"
  declare -a args

  echo "PYTHON ${k}" | colorize green

  if ! "$python" -m pip --version |& colorize black bold; then
    echo "  skipping" | colorize yellow | offset
    continue
  fi

  cd ~/.local/share/tools-update/pip

  # These are important and should be done first.
  "$python" -m pip install --user --upgrade pip wheel | offset

  "$python" \
    -m pip \
    install \
    --user \
    --upgrade \
    --requirement ./requirements.txt \
    | offset
done

# EOF
