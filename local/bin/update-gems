#!/usr/bin/env zsh
# vi: ft=zsh :

set -eu

DOTFILES_DIR=~/.dotfiles
source "${DOTFILES_DIR}/local/include/hook-helpers.zsh"

function trap_exit
{
  if [[ -n "${update_tmp:-}" && -d "${update_tmp}" ]]; then
    rm -rf "${update_tmp}"
  fi
}

trap trap_exit EXIT

function verify_rubygems_or_exit
{
  local needs_update=0
  "$HOME/.bin/rubygems_version_check" || needs_update=$?

  case "$needs_update" in
    0)
      return 0
      ;;
    1)
      echo 'Minor update is available if you want:' | offset | colorize yellow
      echo '  gem update --system' | offset
      return 0
      ;;
    2)
      echo 'Consider upgrading rubygems:' | offset | colorize red
      echo '  gem update --system' | offset
      return 0
      ;;
    *)
      echo 'You should upgrade rubygems!' | offset | colorize red
      echo '  gem update --system && gem pristine --all' | offset | colorize yellow
      return $needs_update
      ;;
  esac
}

function do_update
{
  cp ~/.config/personal/Gemfile.favorites Gemfile

  # Add builtin gems
  ruby='
    Gem::Specification
      .select(&:default_gem?)
      .map(&:name)
      .each do |gem|
        puts "gem #{gem.inspect}"
    end
  '
  ruby -e "${ruby}" >> Gemfile

  bundle install --jobs=3 --retry=3
}

## Main ##

# Set up the temp directory.
update_tmp="$(mktemp -d "${TMPDIR:-/tmp}/update-gems.XXXXXXXXXX")"
cd "${update_tmp}"

echo 'Checking rubygems:' | colorize blue
verify_rubygems_or_exit

echo "Verifying bundler is installed:" | colorize blue
gem install --conservative bundler | offset | colorize green

echo "Installing gems:" | colorize blue
do_update

echo "Indexing documentation:" | colorize blue
yard gems --quiet

# EOF
