#!/bin/zsh

#DESC# fonts for macOS #DESC#

set -euo pipefail

cd "$(dirname "$0")"

if curl --help all | grep -q etag; then
  curl_has_etag=1
else
  curl_has_etag=0
fi
declare -ir curl_has_etag

fetch_or_update() {
  local -r url=$1 destination=$2
  if ! [[ -r "$destination" ]]; then
    curl -sSfL -o "$destination" "$url"
  elif (( curl_has_etag )); then
    curl -sSfL --etag-save "$destination".etag --etag-compare "$destination".etag -o "$destination" "$url"
  else
    curl -sSfL -z "$destination" -o "$destination" "$url"
  fi
}

## Main

if [[ "${OSTYPE}" != darwin* ]]; then
  print -P "  %F{yellow}Skipping%F{reset}: Only %F{green}macOS%F{reset} supported (not %F{green}${OSTYPE}%F{reset})"
  exit 0
fi

rm -f ~/Library/Fonts/NerdFonts/*(N.) ~/Library/Fonts/TerminalAppFonts/(N.)
mkdir -p ~/Library/Fonts/NerdFonts ~/Library/Fonts/TerminalAppFonts/

fetch_or_update \
  "https://github.com/ryanoasis/nerd-fonts/raw/master/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete.ttf" \
  ~/Library/Fonts/NerdFonts/"Symbols Nerd Font.ttf"

# Install SF-Mono fonts for general usage.
terminal_font_dir=/Applications/Utilities/Terminal.app/Contents/Resources/Fonts/
if ! [[ -d $terminal_font_dir ]]; then
  terminal_font_dir="/System/${terminal_font_dir}"
fi
declare -r terminal_font_dir
rsync -avP --chmod=Fa-x --inplace --delete "$terminal_font_dir" ~/Library/Fonts/TerminalAppFonts/

# EOF
