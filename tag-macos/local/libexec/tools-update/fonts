#!/bin/zsh

#DESC# fonts for macOS #DESC#

set -euo pipefail

cd "$(dirname "$0")"

cachedir="$HOME/.cache/tools-update/fonts"

typeset -a fonts
fonts=(
FiraCode
Hack
SourceCodePro
LiberationMono
)

declare -r fonts cachedir

fetch_or_update() {
  local url destination
  url=$1
  destination=$2
  if [ ! -r "$destination" ]; then
    curl -sSfL -o "$destination" "$url"
  else
    curl -sSfL -z "$destination" -o "$destination" "$url"
  fi
}

## Main

if [[ "${OSTYPE}" != darwin* ]]; then
  print -P "  %F{yellow}Skipping%F{reset}: Only %F{green}macOS%F{reset} supported (not %F{green}${OSTYPE}%F{reset})"
  exit 0
fi

if ! (( $+commands[jq] )); then
  print -P "  %F{yellow}Skipping%F{reset}: Install %F{green}jq%F{reset} to install fonts."
  exit 0
fi

release=$(curl -sSfn "https://api.github.com/repos/ryanoasis/nerd-fonts/releases/latest" | jq -r '.tag_name')
declare -r release

print -P "Fetching Nerd Fonts %F{green}${release}%F{reset}"

mkdir -p "$cachedir" ~/Library/Fonts/NerdFonts

for file in ~/Library/Fonts/NerdFonts/*(.N); do
  rm "${file}"
done

for font in "${fonts[@]}"; do
  zipfile="${cachedir}/${font}.zip"
  fetch_or_update \
    "https://github.com/ryanoasis/nerd-fonts/releases/download/${release}/${font}.zip" \
    "${zipfile}"
  unzip -uo "${zipfile}" '*.otf' -x '*Windows*' '*Mono.*' -d ~/Library/Fonts/NerdFonts \
  || unzip -uo "${zipfile}" '*.ttf' -x '*Windows*' '*Mono.*' -d ~/Library/Fonts/NerdFonts
done

# Install SF-Mono fonts for general usage.
rsync -av --delete /Applications/Utilities/Terminal.app/Contents/Resources/Fonts ~/Library/Fonts/TerminalAppFonts/

# EOF
