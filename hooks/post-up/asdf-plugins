#!/usr/bin/env zsh

set -eu

setopt no_ksh_arrays

print -P '%F{blue}asdf plugins...%F{reset}'

if ! grep -q -E "TAGS=.*[\"' ]asdf[\"' ]" ~/.rcrc; then
  print 'skipped'
  exit 0
fi

needed_plugins=(
  # get list of plugins from ~/.tool-versions
  $(awk '{print $1}' ~/.tool-versions)
)

given_plugins=(
  # get currently installed plugins
  $(asdf plugin-list)
)

ASDF_DIR="${ASDF_DIR:-${HOME}/.asdf}"

if [[ -f "${ASDF_DIR}/asdf.sh" ]]; then
  # shellcheck disable=SC1091
  . "${ASDF_DIR}/asdf.sh"

  for plugin in "${needed_plugins[@]}"; do
    if [[ "${given_plugins[(i)${plugin}]}" -le "${#given_plugins}" ]]; then
      print -P "%F{green}✓ %F{reset}${plugin}"
    else
      print -P "%F{yellow}⚠ %F{reset}${plugin}"
      asdf plugin-add "${plugin}" 2>/dev/null
      asdf install "${plugin}" 2> /dev/null
    fi
  done
fi

# EOF
