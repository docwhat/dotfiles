#!/usr/bin/env zsh
#
# Usage: add-nodejs-cli-tool <npm-package> [<command-name>]
#    or: add-nodejs-cli-tool <existing-nodejs-cli-tool>

set -eu

declare -r node_version=8

function error {
  print -P "%B%F{red}ERROR%f%b: $*"
  exit 10
}

export NODENV_VERSION=$node_version

if [ -z "${1:-}" ] || [[ "${1:-}" = -* ]]; then
  echo "Usage: $0 <npm-package> [<command-name>]"
  exit 0
fi

stub=~/.dotfiles/tag-nodejs/bin/"$(basename "$1")"

if [ -x "$stub" ]; then
  # if ! grep -q '##marker: NODEJS-CLI-TOOL##' "$stub"; then
  #   error "$stub is not a node-cli stub".
  # fi
  eval "$(grep -E '^(package|command)=' "$stub")"
  if [ -z "${package:-}" ] && [ -z "${command:-}" ]; then
    error "$stub is a malformed node-cli stub".
  fi
  declare -r package command stub update=1
  print -P "%F{green}Updating%f $command (from $package)"
else
  declare -r package="$1"
  declare -r command="${2:-$1}"
  declare -r stub=~/.dotfiles/tag-nodejs/bin/"$command"
  declare -r update=0

  print -P "%F{yellow}Creating%f $command (from $package)"
fi

npm install --global "$package" >/dev/null 2>&1

if [ ! -x "$(npm bin --global 2>/dev/null)/${command}" ]; then
  error "cannot find $command in $package"
fi

node ~/.dotfiles/tag-nodejs/share/node-cli-template.js\
  "$package" \
  "$command" \
  "$node_version" \
  > "$stub"
chmod +x "$stub"

if ! (( update )); then
  rcup -K
fi

print -P '%B%F{green}done%f%b'

# EOF
